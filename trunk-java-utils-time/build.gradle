plugins {
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.2"
}

dependencies {
    compile 'joda-time:joda-time:2.9.1'
    testCompile 'org.functionaljava:functionaljava:4.5'
    testCompile 'org.functionaljava:functionaljava-java-core:4.5'
    testCompile 'org.functionaljava:functionaljava-java8:4.5'
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

apply from: 'bintray.gradle'